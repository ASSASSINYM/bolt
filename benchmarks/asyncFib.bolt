class FibHelper{
  capability linear left, linear right, read view;
  var int left : left;
  var int right : right;
  const int n : view;
}

function int fib(int n){
  if ((n==0) || (n==1)){
    1
  } else{
    (fib(n - 1)) + (fib(n - 2))
    }
}


function int asyncFib(int n){
  if(n <35) { // for small cases use synchronous version
      fib(n)
  }
  else{
    let x = new FibHelper(n : n);
    finish{
      async{
        x.left :=  asyncFib(x.n - 1)
      }
      x.right :=  asyncFib(x.n - 2)
    };
    x.left + x.right
  }
}

void main(){
  printf("Fib of 46 is: %d", asyncFib(46) )
}