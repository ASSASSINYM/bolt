class Tree {
  capability linear Left, linear Val, linear Right, read View, read ViewElem;
  var Tree leftChild : Left, View;
  var int value : Val, ViewElem; 
  var Tree rightChild: Right, View;

  void setLeftChildVal(int val) : Left {
      let y = consume this.leftChild;
      y.value := val;
      this.leftChild := consume y

  }
  void setRightChildVal(int val) : Right {
      let y = consume this.rightChild;
      y.value := val;
      this.rightChild := consume y
  }
}


function int fib(int n){
  if ((n==0) || (n==1)){
    1
  } else{
    (fib(n - 1)) + (fib(n - 2))
  }
}


void main(){
  printf("Fibonacci of 5 is: %d",fib(5));
  let left = new Tree();
  let right = new Tree();
  let x = new Tree(value: 10, leftChild: left, rightChild: right);
  finish{
    async{
      x.setLeftChildVal(5)
    }
    async{
      x.setRightChildVal(50)
    }
    x.value := 20
  };
   finish{
     /* Using the View capability for read-only access MRSW */
    async{
     let y = x.leftChild;
     printf("I can print out the left child in one thread %d" ,y.value)
    }
    async{
     let y = x.leftChild;
     printf("I can print out the left child in one thread %d" ,y.value)
    }
    async{
      let y = x.rightChild;
      printf("I can print out the right child in another thread %d", y.value)
    }
    x.value := 20
  }
}
